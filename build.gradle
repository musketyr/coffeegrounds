plugins {
    id "com.jfrog.bintray" version "0.6"
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'

group = "cz.orany"
version = "0.1"
description = "An awesome groovy library"

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.7'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

// code below this can be safely removed if you don't want publish library ever


publishing {
    publications {
        groovyMaven(MavenPublication) {
            from components.java
        }
    }
}

// set bintrayUser & bintrayKey in gradle.properties
bintray {

    user = getPropertyOrUseDefault("bintrayUser", "fake_user")
    key = getPropertyOrUseDefault("bintrayKey", "fake_key")
    publications = ['groovyMaven']

    def projectName = project.name
    def projectDescription = project.description

    pkg {
        repo = 'maven' // or your repo name
        name = projectName      // somehow project.* doesn't work in this closure
        desc = projectDescription
        licenses = ['Apache-2.0']
    }
    dryRun = true // whether to run this as dry-run, without deploying
}

String getPropertyOrUseDefault(String propertyName, String defaultValue) {
    hasProperty(propertyName) ? getProperty(propertyName) : defaultValue
}
